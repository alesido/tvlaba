//
// Laboratory project for Set Top Box & Smart TV, i.e. big screen
//

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs"

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion 29

    defaultConfig {

        applicationId "org.alsi.android.tvlaba.tv"

        minSdkVersion globalConfiguration["androidMinSdkVersion"]
        targetSdkVersion globalConfiguration["androidTargetSdkVersion"]

        versionCode 1
        versionName "1.0.1"

        multiDexEnabled = true
        vectorDrawables.useSupportLibrary = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias 'tvlaba'
            keyPassword 'tvlaba'
            storeFile file('../tvlaba.keystore')
            storePassword 'tvlaba'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "$JavaVersion.VERSION_1_8"
    }

    androidExtensions {
        experimental = true
    }
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == "android.support") {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "26.+"
            }
        }
    }
}


dependencies {
    def uiDependencies = rootProject.ext.uiDependencies
    def uiTestDependencies = rootProject.ext.uiTestDependencies

    implementation project(':Domain')
    implementation project(':PresentationTv')
    implementation project(':MoiDom')

    //Support & Compatibility
    implementation uiDependencies.androidSupportV4
    implementation uiDependencies.androidSupportV13
    implementation uiDependencies.appCompatV7

    //Design & Layout
    implementation uiDependencies.supportRecyclerView
    implementation uiDependencies.supportDesign
    implementation uiDependencies.constraintLayout

    //Architecture Components
    implementation uiDependencies.archRuntime
    implementation uiDependencies.archExtensions
    kapt uiDependencies.archCompiler

    //Kotlin
    implementation uiDependencies.kotlin
    //implementation 'androidx.core:core-ktx:1.3.1'

    //RX
    implementation uiDependencies.rxKotlin
    implementation uiDependencies.rxAndroid
    implementation domainDependencies.rxJava

    //Dependency Injection
    implementation uiDependencies.javaxInject
    implementation uiDependencies.dagger
    implementation uiDependencies.daggerSupport
    kapt uiDependencies.daggerCompiler
    kapt uiDependencies.daggerProcessor

    //Annotations
    implementation uiDependencies.javaxAnnotation
    implementation uiDependencies.androidAnnotations
    compileOnly uiDependencies.glassfishAnnotation

    //Leanback & ExoPlayer
    implementation uiDependencies.leanback
    implementation uiDependencies.exoplayer
    implementation uiDependencies.exoplayerLeanback

    //Navigation of Jet Pack
    def nav_version = '2.3.0'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    //Miscellaneous
    implementation uiDependencies.timber
    implementation uiDependencies.glide

    //Unit Testing
    testImplementation uiTestDependencies.kotlinJUnit

    kaptTest uiDependencies.daggerCompiler
    kaptAndroidTest uiDependencies.daggerCompiler

    //Instrumentation Testing
    androidTestImplementation uiTestDependencies.junit
    androidTestImplementation uiTestDependencies.mockito
    androidTestImplementation uiTestDependencies.mockitoAndroid
    androidTestImplementation(uiTestDependencies.espressoCore) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(uiTestDependencies.androidRunner) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(uiTestDependencies.androidRules) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(uiTestDependencies.espressoIntents) {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation(uiTestDependencies.espressoContrib) {
        exclude module: 'appcompat'
        exclude module: 'appcompat-v7'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'support-annotations'
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
    }
}
